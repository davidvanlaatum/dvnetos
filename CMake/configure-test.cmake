function(configure_test TARGET)
  target_link_libraries(${TARGET} PRIVATE GTest::gtest_main)
  if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang" OR CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
    if (CMAKE_SYSTEM_NAME STREQUAL "Linux")
      target_link_libraries(${TARGET} PRIVATE stdc++ m)
    elseif (CMAKE_SYSTEM_NAME STREQUAL "Darwin")
      target_link_libraries(${TARGET} PRIVATE c++)
    else ()
      message(FATAL_ERROR "Unsupported operating system: ${CMAKE_SYSTEM_NAME}")
    endif ()
  elseif (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
    target_link_libraries(${TARGET} PRIVATE stdc++)
  else ()
    message(FATAL_ERROR "Unsupported compiler: ${CMAKE_CXX_COMPILER_ID}")
  endif ()
  target_link_options(${TARGET} PRIVATE ${COVERAGE_LINKER_OPTIONS})
  target_compile_options(${TARGET} PRIVATE ${COVERAGE_CXX_FLAGS})
  set_target_properties(${TARGET} PROPERTIES IS_TEST_CASE ON)
  get_target_property(CLEAN ${TARGET} ADDITIONAL_CLEAN_FILES)
  if (NOT CLEAN)
    set(CLEAN "")
  endif ()
  list(APPEND CLEAN "${CMAKE_CURRENT_BINARY_DIR}/coverage/${TARGET}")
  set_target_properties(${TARGET} PROPERTIES ADDITIONAL_CLEAN_FILES "coverage/${TARGET};default.profraw" COVERAGE_FILES "${CMAKE_CURRENT_BINARY_DIR}/coverage/${TARGET}/*.profraw")
  gtest_discover_tests(${TARGET} PROPERTIES ENVIRONMENT "LLVM_PROFILE_FILE=${CMAKE_CURRENT_BINARY_DIR}/coverage/${TARGET}/%p.profraw")
endfunction()
