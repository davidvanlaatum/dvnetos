include(CheckCompilerFlag)
include(CheckLinkerFlag)

function(add_compile_options_if_supported lang flag addToVar)
    string(REGEX REPLACE "[^A-Za-z_]+" "_" VAR "${lang}_COMPILER_SUPPORTS${flag}")
    check_compiler_flag(${lang} "-Werror ${flag}" ${VAR})
    if (${VAR})
        list(APPEND ${addToVar} "${flag}")
        set(${addToVar} "${${addToVar}}" PARENT_SCOPE)
    endif ()
endfunction()

function(add_linker_options_if_supported lang flag addToVar)
    string(REGEX REPLACE "[^A-Za-z_]+" "_" VAR "${lang}_LINKER_SUPPORTS${flag}")
    check_compiler_flag(${lang} "${flag}" ${VAR})
    if (${VAR})
        list(APPEND ${addToVar} "${flag}")
        set(${addToVar} "${${addToVar}}" PARENT_SCOPE)
    endif ()
endfunction()

function(getAllSubdirs dir dirs)
    get_property(subdirs DIRECTORY ${dir} PROPERTY SUBDIRECTORIES)
    foreach (subdir ${subdirs})
        list(APPEND ${dirs} ${subdir})
        getAllSubdirs(${subdir} ${dirs})
    endforeach ()
    set(${dirs} ${${dirs}} PARENT_SCOPE)
endfunction()

function(getAllTargets var)
    set(DIRS ${CMAKE_SOURCE_DIR})
    getAllSubDirs(. DIRS)
    foreach (dir ${DIRS})
        string(FIND ${dir} "${CMAKE_BINARY_DIR}/_deps" IS_DEPS_DIR)
        if (IS_DEPS_DIR EQUAL 0)
            continue()
        endif ()
        get_property(targets DIRECTORY ${dir} PROPERTY BUILDSYSTEM_TARGETS)
        foreach (target ${targets})
            get_target_property(TYPE ${target} TYPE)
            if (TYPE STREQUAL "UTILITY")
                continue()
            endif ()
            list(APPEND ${var} ${target})
        endforeach ()
    endforeach ()
    set(${var} ${${var}} PARENT_SCOPE)
endfunction()
